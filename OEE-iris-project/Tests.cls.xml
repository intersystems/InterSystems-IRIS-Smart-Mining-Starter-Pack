<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="MDX2JSON.Tests">
<Description>
Classes with support methods for testing MDX2JSON</Description>
<IncludeCode>MDX2JSON.MDX2JSON</IncludeCode>
<TimeCreated>63636,71862.541637</TimeCreated>

<Parameter name="LastCommit">
<Type>%String</Type>
<Default>57ad7052f14c03ab631526905533f80b42674c08</Default>
</Parameter>

<Parameter name="LastCommitTS">
<Type>%TimeStamp</Type>
<Default>2018-03-27 08:46:22.021</Default>
</Parameter>

<Method name="ProjectLength">
<Description>
Count project length in loc
do ##class(MDX2JSON.Tests).ProjectLength()</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>Mask:%String="MDX2JSON"</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	 Set rset = ##class(%ResultSet).%New("%DynamicQuery:SQL")
	 Do rset.Prepare("SELECT Name FROM %Dictionary.ClassDefinition WHERE NOT Name %STARTSWITH '%sqlcq' AND Name [ '"_Mask_"'")
	 Do rset.Execute()
	 Set count = 0

	 While (rset.Next()) {
		 Set class = rset.Data("Name")
		 Do ##class(%Compiler.UDL.TextServices).GetTextAsArray($Namespace, class, .raw)
		 Set lines = $O(raw($C(0)), -1)
		 Write class, $JUSTIFY("",30-$L(class)), $J(lines,5), !
		 Set count = count + lines
	 }
	 Write "Total ", $JUSTIFY("",24), $J(count,5)
]]></Implementation>
</Method>

<Method name="MoveFoldersIntoMobile">
<Description><![CDATA[
Entry point to move dashboards into mobile folder<br>
Moves all DeepSee folders into Mobile folder (creates folder, if required)]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>Namespace=$Namespace</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set mobileid =..CreateMobileFolder(Namespace)
	Set ns =$namespace
	Zn Namespace
	&sql(UPDATE %DeepSee_UserLibrary.Folder SET folder=:mobileid WHERE folder is null AND name != 'Mobile')
	Zn ns
	Do ##class(MDX2JSON.Tests).MoveWidgetsDSIntoMobile(Namespace)
	Return SQLCODE
]]></Implementation>
</Method>

<Method name="CreateMobileFolder">
<Description><![CDATA[
Creates DeepSee folder Mobile in <b>Namespace</b><br>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>Namespace=$Namespace</FormalSpec>
<ReturnType>%Integer</ReturnType>
<Implementation><![CDATA[
	Set ns =$namespace
	Zn Namespace
	&sql(SELECT ID into :ID FROM %DeepSee_UserLibrary.Folder WHERE folder is null AND name = 'Mobile')
	Zn ns
	Return:$ISVALIDNUM(ID) ID

	Set folder = ##class(%DeepSee.UserLibrary.Folder).%New()
	Set folder.name = "Mobile"
	W $System.Status.GetErrorText(folder.%Save())

	Return folder.%Id()
]]></Implementation>
</Method>

<Method name="MoveWidgetsDSIntoMobile">
<Description>
Moves widgets datasources into Mobile folder</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>Namespace=$Namespace</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set ns =$namespace
	Zn Namespace
		&sql(DECLARE sql1 CURSOR FOR
		SELECT ID INTO :ID FROM %DeepSee_Dashboard.Definition)

	&sql(OPEN sql1)
	&sql(FETCH sql1)
	While (SQLCODE = 0) {
		Set Dashboard = ##class(%DeepSee.Dashboard.Definition).%OpenId(ID,,.st)
		For i=1:1:Dashboard.widgets.Count() {
			Set pivot = Dashboard.widgets.GetAt(i).dataSource
			If ((##class(%DeepSee.UserLibrary.Utils).%FolderItemExists(pivot)=0) && (##class(%DeepSee.UserLibrary.Utils).%FolderItemExists("Mobile/"_pivot)=1))
			{
				Set Dashboard.widgets.GetAt(i).dataSource = "Mobile/"_pivot
			}
		}
		Do Dashboard.%Save()
		Set Dashboard=""
		&sql(FETCH sql1)
	}
	&sql(CLOSE sql1)

	Zn ns
	Return $$$OK
]]></Implementation>
</Method>

<Method name="DynamicObjects">
<Description>
do ##class(MDX2JSON.Tests).DynamicObjects()</Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	set obj = $$$NewDynObj
	set obj.prop = "val"
	$$$DynObjToJSON(obj)

	set dtList = $$$NewDynDTList
	set a = 1
	$$$Insert(dtList,a)
	$$$Insert(dtList,"a")
	$$$ListToJSON(dtList)
]]></Implementation>
</Method>

<Method name="RenderSpeed">
<Description>
do ##class(MDX2JSON.Tests).RenderSpeed()</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>MDX:%String,Count:%Integer(MINVAL=1)=10</FormalSpec>
<Implementation><![CDATA[
	Set:$d(MDX)'=1 MDX = "SELECT NON EMPTY [Product].[P1].[Product Category].Members ON 0,NON EMPTY [Outlet].[H1].[Region].Members ON 1 FROM [HoleFoods]"
	Set RS = ##class(MDX2JSON.Utils).GetResultSet(MDX,.st)
	Write $System.Status.GetErrorText(st)
	Set obj = RS.ToProxyObject(.st)
	Write $System.Status.GetErrorText(st)
	Set od=$io
	Set nul=##class(%Device).GetNullDevice() //"\\.\nul" // /dev/null/ - UNIX
	Open nul
	Use nul

	Set startTime = $NOW()
	For i = 1:1:Count {
		$$$DynObjToJSON(obj)
	}
	Set endTime = $NOW()

	Set time = $p(endTime,",",*) - $p(startTime,",",*)
	Use od
	Close nul
	Write "Iterations: ",Count,!
	Write "Time Total ",time,!
	Write "Time Average: ",time/Count
]]></Implementation>
</Method>

<Method name="MakeWidgetKeyUnique">
<Description>
Iterate over all dashboards in a namespace, for each iterate over widgets.
Changes widget key if previously encountered</Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	set rs = ##class(%DeepSee.Dashboard.Definition).ExtentFunc()
	set WidgetKeyList=""
	while rs.%Next() {
		 set Dash = ##class(%DeepSee.Dashboard.Definition).%OpenId(rs.%Get("ID"))
		 for i=1:1:Dash.widgets.Count() {
			 set WidgetKey = Dash.widgets.GetAt(i).key
			 if $LF(WidgetKeyList,WidgetKey) {
				 write "Duplicate dash: ", rs.%Get("ID"), " oldkey: ", WidgetKey
				 // generate new randow widget key
				 do {
				 	set WidgetKey = $R(1000000000)
				 } while ($LF(WidgetKeyList,WidgetKey)'=0)
				 set WidgetKeyList = WidgetKeyList _$LB(WidgetKey)
				 set Dash.widgets.GetAt(i).key = WidgetKey
				 write "newkey: ", WidgetKey, !
				 write $System.Status.GetErrorText(Dash.%Save())
			 } else {
				 set WidgetKeyList = WidgetKeyList _$LB(WidgetKey)
			 }
		 }
   }
]]></Implementation>
</Method>

<Method name="OnCommit">
<Description><![CDATA[
Hook for Cache-Tort-Git source-control plug-in.<br>
Sets LastCommit parameter to last successful commit and exports this class.]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>Command,InternalName</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	return:Command'="commit" $$$OK
	set Status = ..SetParameter(, "LastCommit", ..GetLastCommitHash())
	return:$$$ISERR(Status) Status
	set Status = ..SetParameter(, "LastCommitTS", $ZDT($ZTIMESTAMP, 3, 1, 3))
	return:$$$ISERR(Status) Status
	do ##class(%SourceControl.Git.Utils).UpdateRoutineTSH("MDX2JSON.Tests.cls")
	return ##class(%SourceControl.Git.Utils).ExportItem("MDX2JSON.Tests.cls")
]]></Implementation>
</Method>

<Method name="GetLastCommitHash">
<Description><![CDATA[
This method returns last commit SHA-1 hash of <var>Branch</var>
in Git repository in <var>RepoFolder</var>.<br>
To get current namespace repository you need to use
Cache-Tort-Git source-control plug-in]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>RepoFolder:%String=##class(%SourceControl.Git.Utils).TempFolder(),Branch="master"</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	set GitDir = ##class(%File).SubDirectoryName(RepoFolder,".git")
	set RefDir = ##class(%File).SubDirectoryName(GitDir,"refs")
	set HeadRefDir = ##class(%File).SubDirectoryName(RefDir, "heads", 1)
	set HeadRefFileName = HeadRefDir _ Branch
	set HeadRefFile = ##class(%File).%New(HeadRefFileName)
	do HeadRefFile.Open("R")
	set SHAHash = HeadRefFile.ReadLine()
	do HeadRefFile.Close()
	return SHAHash
]]></Implementation>
</Method>

<Method name="SetParameter">
<Description><![CDATA[
Set Class Parameter equal to Value<]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>Class:%String=$classname(),Parameter:%String,Value:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set Parameter = ##class(%Dictionary.ParameterDefinition).IDKEYOpen(Class, Parameter)
	set Parameter.Default = Value
	return Parameter.%Save()
]]></Implementation>
</Method>
</Class>
</Export>
